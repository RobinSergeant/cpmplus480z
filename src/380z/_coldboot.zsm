; Copyright (C) 2024 Robin Sergeant
;
; cold boot loader

CONTC		EQU 0
OUTC		EQU 1
MSG		EQU 23
RDSEC		EQU 26
VERSN		EQU 51
WIDTH		EQU 52

LDR_LEN		EQU 22

		CSEG
		ORG 0

		LD SP,4000H

		LD IX,unit
		LD IY,xlt
		LD DE,128
		LD HL,0100H
		LD B,LDR_LEN

read_loop:	EMT RDSEC
		JR NZ,error
		LD A,(IY+0)		; set next sector
		INC IY
		LD (sector),A
		ADD HL,DE		; move destination buffer
		LD (address),HL
		DJNZ read_loop

		LD A,17			; prevent autopaging
		EMT OUTC

		EMT VERSN		; 80 column mode only supported
		CP 40			; from COS 4.0 onwards
		JP C,0100H
		LD A,1			; set 80 column mode
		EMT WIDTH

		JP 0100H		; transfer control to loader

error:		LD HL,err_msg
		EMT MSG
		EMT CONTC		; back to ROS

xlt:		DEFB 13,19,25
		DEFB 5,11,17,23
		DEFB 3,9,15,21
		DEFB 2,8,14,20,26
		DEFB 6,12,18,24
		DEFB 4,10,16

unit:		DEFB 00000000B
track:		DEFB 0
sector:		DEFB 7
address:	DEFW 0100H

err_msg:	DEFB 13
		DEFM 'Disc error while loading CPMLDR.COM'
		DEFB -1
