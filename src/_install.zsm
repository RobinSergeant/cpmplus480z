; Copyright (C) 2024 Robin Sergeant
;
; install program

WRSECL		EQU 69
FLUSH		EQU 71
RDINFO		EQU 72

BDOS		EQU 0005H
FN_PRINT	EQU 9
FN_OUT		EQU 2
FN_OPEN_FILE	EQU 15
FN_READ_SEQ	EQU 20
FN_DRV_GET	EQU 25
FN_SET_DMA	EQU 26

BUF_START	EQU 1000H
BOOT_UNIT_LOC	EQU BUF_START + 0032H
LDR_UNIT_LOC	EQU BUF_START + 0AC0H
LDR_DPB_LOC	EQU BUF_START + 0AA3H

		COM
		ORG 100H

		LD (saved_sp),SP
		LD SP,stack

		LD C,FN_DRV_GET		; output to selected drive
		CALL BDOS
		LD (unit),A

		LD IX,unit
		EMT RDINFO		; read disc info
		JP NZ,disc_error
		AND 00000110B		; keep data and track density
		RLCA			; shift to bits 5 and 6 to
		RLCA			; match drive unit byte format
		RLCA
		RLCA
		LD (density),A

		CALL read_file		; write cold boot loader to track 0
		LD A,(density)
		LD (BOOT_UNIT_LOC),A	; patch to use correct density
		CALL write_file

		LD A,(unit)		; set density (only track 0 is SD)
		LD HL,density
		OR (HL)
		LD (unit),A

		LD HL,ldr_fcb		; write cpmldr to track 1
		CALL setup_file
		CALL read_file
		LD A,(density)
		BIT 6,A
		JR Z,cont		; only patch for quad density

		LD (LDR_UNIT_LOC),A	; patch unit byte

		LD DE,LDR_DPB_LOC	; replace DPB
		LD HL,dpb_qd
		LD BC,DPB_LEN
		LDIR

cont:		CALL write_file

		LD HL,ccp_fcb		; write ccp to track 2
		CALL setup_file
		CALL read_file
		CALL write_file

exit:		EMT FLUSH		; flush any unwritten data

		LD SP,(saved_sp)
		RET

setup_file:	LD (current_fcb),HL
		LD HL,BUF_START		; reset buffer pointers
		LD (dma_buffer),HL
		LD (address),HL
		LD IX,unit
		INC (IX+1)		; increment track
		LD (IX+2),1		; reset sector to 1 (start of next track)
		LD HL,ok_param		; put new track number in ok string
		INC (HL)
		RET

read_file:	LD C,FN_OPEN_FILE
		LD DE,(current_fcb)
		CALL BDOS
		INC A
		JR Z,not_found

read_loop:	LD C,FN_SET_DMA
		LD DE,(dma_buffer)
		CALL BDOS

		LD C,FN_READ_SEQ
		LD DE,(current_fcb)
		CALL BDOS
		CP 0
		RET NZ			; reached end of file

		LD HL,sector_count
		INC (hl)
		LD HL,(dma_buffer)
		LD BC,128
		ADD HL,BC
		LD (dma_buffer),HL
		JR read_loop

write_file:	LD IX,unit
write_loop:	XOR A
		LD HL,sector_count
		CP (HL)
		JR Z,done
		DEC (HL)

		EMT WRSECL
		JR NZ,disc_error

		INC (IX+2)		; increment sector
		LD HL,(address)
		LD BC,128
		ADD HL,BC
		LD (address),HL

		JR write_loop

not_found:	LD C,FN_PRINT
		LD DE,not_found_msg
		CALL BDOS
		JP exit

disc_error:	LD C,FN_PRINT
		LD DE,error_msg
		CALL BDOS
		JP exit

done:		LD C,FN_PRINT
		LD DE,ok_msg
		CALL BDOS
		RET

cold_fcb:	DEFB 0
		DEFM 'COLDBOOT'
		DEFM 'COM'
		DEFS 21

ldr_fcb:	DEFB 0
		DEFM 'CPMLDR  '
		DEFM 'COM'
		DEFS 21

ccp_fcb:	DEFB 0
		DEFM 'CCP     '
		DEFM 'COM'
		DEFS 21

dma_buffer:	DEFW BUF_START
sector_count:	DEFB 0
current_fcb:	DEFW cold_fcb

not_found_msg:	DEFM 'File not found'
		DEFB 13,'$'

error_msg:	DEFM 'Disc error'
		DEFB 13,'$'

ok_msg:		DEFM 'Track '
ok_param:	DEFM '0 written'
		DEFB 13,'$'

density:	DEFB 0

unit:		DEFB 0
track:		DEFB 0
sector:		DEFB 1
address:	DEFW BUF_START

dpb_qd:		DEFW 36		; SPT
		DEFB 4		; BSH
		DEFB 15		; BLM
		DEFB 1		; EXM
		DEFW 172	; DSM
		DEFW 127	; DRM
		DEFB 0C0H	; AL0
		DEFB 0		; AL1
		DEFW 8000H	; CKS (not required)
		DEFW 3		; OFF
		DEFB 0		; PSH
		DEFB 0		; PHM
DPB_LEN		EQU $ - dpb_qd

saved_sp:	DEFS 2

		DEFS 32
stack:
